pipeline {
    agent any

    parameters {
        choice(name: 'MODE', choices: ['CHANGED_ONLY', 'ALL'], description: 'Chon che do convert: Chi file thay doi (CHANGED_ONLY) hoac Tat ca file (ALL)')
        string(name: 'TARGET_FOLDER', defaultValue: 'Markdown', description: 'Ten thu muc can quet (Vi du: Markdown)')
    }
    
    stages {
        stage('Checkout') {
            steps {
                checkout([
                    $class: 'GitSCM',
                    branches: scm.branches,
                    extensions: [[$class: 'CloneOption', depth: 0, noTags: false, reference: '', shallow: false]],
                    userRemoteConfigs: scm.userRemoteConfigs
                ])
            }
        }
        
        stage('Check Commit Message & Filter') {
            steps {
                script {
                    def manualCauses = currentBuild.getBuildCauses('hudson.model.Cause$UserIdCause')
                    def isManual = manualCauses != null && manualCauses.size() > 0
                    
                    // Logic moi: Quet toan bo changeLog de tim file va check message
                    def changedFilesBuffer = []
                    def hasDocMessage = false
                    
                    // Lay tat ca change sets (cac commit trong lan push nay)
                    def changeLogSets = currentBuild.changeSets
                    
                    if (changeLogSets.size() == 0) {
                         // Truong hop build lan dau hoac Force Build khong co change log
                         // Fallback check commit cuoi cung
                         def lastMsg = sh(returnStdout: true, script: 'git log -1 --pretty=%s').trim()
                         if (lastMsg ==~ /^doc(\\(.*\\))?: .*/) hasDocMessage = true
                    } else {
                        for (int i = 0; i < changeLogSets.size(); i++) {
                            def entries = changeLogSets[i].items
                            for (int j = 0; j < entries.length; j++) {
                                def entry = entries[j]
                                
                                // 1. Kiem tra xem co commit nao bat dau bang doc: khong
                                if (entry.msg ==~ /^doc(\\(.*\\))?: .*/) {
                                    hasDocMessage = true
                                }
                                
                                // 2. Nhat file .md thay doi
                                def files = entry.affectedFiles
                                for (int k = 0; k < files.size(); k++) {
                                    def file = files[k]
                                    if (file.path.startsWith("${params.TARGET_FOLDER}/") && file.path.endsWith(".md")) {
                                        changedFilesBuffer.add(file.path)
                                    }
                                }
                            }
                        }
                    }

                    // Quyet dinh co chay tiep hay khong
                    if (isManual) {
                        echo "Manual build. Ignoring commit message checks."
                    } else {
                        if (hasDocMessage) {
                            echo "Found at least one 'doc:' commit. Proceeding..."
                        } else {
                            currentBuild.result = 'ABORTED'
                            error("Skipping build: No commit in this push starts with 'doc:'")
                        }
                    }
                    
                    // Luu danh sach file tim duoc vao bien moi truong de dung o stage sau
                    env.GROOVY_DETECTED_FILES = changedFilesBuffer.join(" ")
                }
            }
        }
        
        stage('Convert & Push PDF') {
            agent {
                docker { 
                    image 'pandoc/latex:latest' 
                    args '-u root:root --entrypoint='
                    reuseNode true
                }
            }
            steps {
                withCredentials([usernamePassword(credentialsId: 'github-key', passwordVariable: 'GIT_PASS', usernameVariable: 'GIT_USER')]) {
                    script {
                        sh """
                            ORIG_UID=\$(stat -c "%u" .)
                            ORIG_GID=\$(stat -c "%g" .)
                            
                            TARGET="${params.TARGET_FOLDER}"
                            MODE="${params.MODE}"

                            apk add --no-cache ttf-dejavu sed git librsvg || true
                            
                            git config --global --add safe.directory "*"
                            git config --global user.email "jenkins-bot@localhost"
                            git config --global user.name "Jenkins PDF Bot"
                            git remote set-url origin https://\${GIT_USER}:\${GIT_PASS}@github.com/shiroizdabezt/TestForBestarion.git

                            ESC=\$(printf '\\033')
                            
                            FILES_LIST=""

                            if [ "\$MODE" = "ALL" ]; then
                                echo "Mode: ALL. Scanning all files in \$TARGET..."
                                FILES_LIST=\$(find "\$TARGET" -name "*.md")
                            else
                                echo "Mode: CHANGED_ONLY."
                                if [ -n "\$GROOVY_DETECTED_FILES" ]; then
                                    echo "Groovy detected changes from changesets."
                                    FILES_LIST="\$GROOVY_DETECTED_FILES"
                                else
                                    echo "No files detected in changeset. Checking HEAD~1 fallback..."
                                    FILES_LIST=\$(git diff --name-only --diff-filter=AM HEAD~1 HEAD | grep "^\$TARGET/.*\\.md\$" || true)
                                fi
                            fi

                            if [ -z "\${FILES_LIST}" ]; then
                                echo "No files found to process."
                            else
                                echo "Files to process:"
                                echo "\${FILES_LIST}"
                                
                                # Loai bo cac file trung lap (neu 1 file bi sua trong nhieu commit)
                                FILES_LIST=\$(echo "\${FILES_LIST}" | tr ' ' '\\n' | sort -u)
                                
                                echo "\${FILES_LIST}" | while read filepath; do
                                    if [ -f "\${filepath}" ]; then
                                        dir=\$(dirname "\${filepath}")
                                        filename=\$(basename "\${filepath}")
                                        
                                        echo "Processing \${filename} in \${dir}..."
                                        
                                        (
                                            cd "\${dir}" || exit
                                            
                                            LC_ALL=C sed "s/\${ESC}//g; s/\${ESC}\\[[0-9;]*[a-zA-Z]//g" "\${filename}" > "\${filename}.clean"
                                            
                                            pandoc "\${filename}.clean" \
                                            -f markdown \
                                            -o "\${filename%.md}.pdf" \
                                            --pdf-engine=xelatex \
                                            -V mainfont="DejaVu Sans" \
                                            -V geometry:margin=2cm \
                                            -V header-includes='\\providecommand{\\e}{}\\providecommand{\\s}{\\textbackslash s}'
                                            
                                            rm "\${filename}.clean"
                                        )
                                    fi
                                done
                                
                                echo "Checking for changes..."
                                find "\$TARGET" -name "*.pdf" | xargs git add
                                
                                if ! git diff-index --quiet HEAD; then
                                    echo "Changes detected. Committing and Pushing..."
                                    git commit -m "chore: auto-generated PDFs (\$MODE)"
                                    git push origin HEAD:main
                                else
                                    echo "No PDF changes detected. Skipping push."
                                fi
                            fi

                            chown -R \${ORIG_UID}:\${ORIG_GID} .
                        """
                    }
                }
            }
        }
    }
}